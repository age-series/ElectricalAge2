buildscript {
    ext.kotlin_version = '1.6.10'
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.1"
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: "com.github.johnrengelman.shadow"
apply from: 'https://raw.githubusercontent.com/thedarkcolour/KotlinForForge/site/thedarkcolour/kotlinforforge/gradle/kff-3.3.2.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = '1.0.1'
group = 'org.eln2.mc'
archivesBaseName = 'eln2'

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'parchment', version: "2022.05.22-1.18.2"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                eln2 {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                eln2 {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'eln2', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                eln2 {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    library
    implementation.extendsFrom library
}

minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

test{
    useJUnitPlatform()
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://maven.bai.lol" }
    maven {
        name "tterrag maven"
        url "https://maven.tterrag.com/"
    }
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven and Maven Central for you

    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so:
    // flatDir {
    //     dir 'libs'
    // }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.1.25'
    compileOnly fg.deobf('mcp.mobius.waila:wthit-api:forge-4.7.2')
    runtimeOnly fg.deobf('mcp.mobius.waila:wthit:forge-4.7.2')
    library('com.github.age-series:libage:next-SNAPSHOT')
    library('org.apache.commons:commons-math3:3.6.1')
    library('org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2')
    library('com.charleskorn.kaml:kaml:0.40.0')
    implementation fg.deobf('com.jozufozu.flywheel:Flywheel-Forge:1.18-0.6.4.87')

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    library 'io.prometheus:simpleclient:0.16.0'
    library 'io.prometheus:simpleclient_hotspot:0.16.0'
    library 'io.prometheus:simpleclient_httpserver:0.16.0'
    library 'io.prometheus:simpleclient_pushgateway:0.16.0'
    // Real mod deobf dependency examples - these get remapped to your current mappings
    // compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api") // Adds JEI API as a compile dependency
    // runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}") // Adds the full JEI mod as a runtime dependency
    // implementation fg.deobf("com.tterrag.registrate:Registrate:MC${mc_version}-${registrate_version}") // Adds registrate as a dependency

    // Examples using mod jars from ./libs
    // implementation fg.deobf("blank:coolmod-${mc_version}:${coolmod_version}")
}

jar {
    //noinspection GroovyAssignabilityCheck
    archiveClassifier = 'slim'
    manifest {
        attributes([
                "Specification-Title"     : "eln2",
                "Specification-Vendor"    : "age-series-team",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "age-series-team",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Do shadow relocations
// (I can't figure out how to do this in an automatic, sensible way, so--for
// now--you will have to hunt down these dependencies manually and iteratively.
// Let ./gradlew dependencies be your guide, and/or the printlns below.
// Sorry!  - Grissess)
def shaded = [
    "com.charleskorn.kaml",
    "org.snakeyaml",
    "org.apache.commons",
    "com.github.age-series"
].stream().collect()

shadowJar {
    archiveClassifier = ''
    dependencies {
        exclude(dependency {
            def res = !shaded.contains(it.moduleGroup)
            if(!res) println("Include: $it")
            else println("Exclude: $it")
            res
        })
    }
    shaded.forEach {
        relocate it, "org.ageseries.shadow." + it
    }

    finalizedBy 'reobfShadowJar'
}

assemble.dependsOn shadowJar

reobf {
    shadowJar { }
}

// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
// publish.dependsOn('reobfJar')
